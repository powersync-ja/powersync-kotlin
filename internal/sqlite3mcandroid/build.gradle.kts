import com.android.build.gradle.tasks.ExternalNativeBuildJsonTask
import kotlin.jvm.java

// This is an Android-only build to be able to use externalNativeBuild, which has been removed from
// KMP projects with AGP version 9.x.

plugins {
    alias(libs.plugins.android.library)
    id("com.powersync.plugins.sonatype")
}

val androidBuildSourceConfiguration by configurations.creating {
    isCanBeConsumed = false
}

dependencies {
    androidBuildSourceConfiguration(project(path=":internal:prebuild-binaries", configuration="androidBuildSourceConfiguration"))

    testImplementation(libs.test.junit)
    androidTestImplementation(libs.androidx.junit)
    androidTestImplementation(libs.androidx.espresso.core)
    androidTestImplementation(projects.sqlite3multipleciphers)
}

android {
    namespace = "com.powersync.encryption.android"
    ndkVersion = "29.0.14206865"

    compileSdk =
        libs.versions.android.compileSdk
            .get()
            .toInt()

    defaultConfig {
        minSdk =
            libs.versions.android.minSdk
                .get()
                .toInt()

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles("consumer-rules.pro")
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    externalNativeBuild {
        cmake {
            path("build/androidJniBuild/CMakeLists.txt")
        }
    }
}

val generateCmake by tasks.registering(Copy::class) {
    from(androidBuildSourceConfiguration)
    into(layout.buildDirectory.dir("androidJniBuild"))
}

tasks.withType(ExternalNativeBuildJsonTask::class.java).configureEach {
    // Android runs these tasks to configure the CMake build. Since the CMake build in this case
    // is generated by a Gradle task (which copies artifacts around), we need to make sure that task
    // runs before CMake is invoked.
    dependsOn(generateCmake)
}

tasks.withType<com.android.build.gradle.tasks.JavaDocGenerationTask> {
    // The Android Gradle plugin ships with a very old version of Dokka that is enabled by default and doesn't support
    // running on Java 25.
    // We don't even have any Kotlin sources here, so we can skip the Android documentation task.
    enabled = false
}
